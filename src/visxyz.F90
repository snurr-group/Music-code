!---------------------------------------------------------------
! This module handles the entries of .xyz files
!----------------------------------------------------------------
Module visxyz

  Use defaults, Only: RDbl, strLen, lstrLen
  Use vector, Only: VecType, vector_display, Assignment(=), Operator(+), &
      Operator(-), Operator(*), Operator(/)
  Use file, Only: file_open

  Implicit None
  Save

  Private
  Public :: XYZ_Entry, visxyz_make, visxyz_dump, visxyz_molabel1, &
      visxyz_molabel2, visxyz_dispset, visxyz_entrydisplay

  Interface visxyz_display
    Module Procedure visxyz_entrydisplay
  End Interface

  Type XYZ_Entry
    Character(len=2)                :: symbol
    Type(VecType)                   :: r
  End Type XYZ_Entry  

  !** setup the debugging molecular labels using CPK/Shaji color scheme
  !** 1:C1:brown, 2:C2:purple, 3:C3:blue, 4:C4:blue, 5:Cl:green
  !** 6:N:light_blue, 7:P:orange, 8:He:light_pink
  Character(len=2), Dimension(8), Parameter :: visxyz_molabel1 = &
      (/'C1','C2','C3','C4','Cl','N ','P ','H3'/)

  !** setup the debugging molecular labels using Rasmol CPK color scheme
  !** The colors of the rainbow, isn't that pretty ?!?
  !** 0:O:red, 1:P:orange, 2:S:yellow, 3:Cl:green, 4:N:light_blue, 5:Na:blue
  !** 6:K:pink, 7:Br:brown, 8:H:white, 9:C:light_gray, 10:Al:dark_gray
  Character(len=2), Dimension(0:10), Parameter :: visxyz_molabel2 = &
      (/'O ','P ','S ','Cl','N ','Na','K ','Br','H ','C ','Al'/)

Contains 

  !----------------------------------------------------------------------------
  ! This function turns information for a single entry into a formal
  ! XYZ entry.  It will use the optional integer label to produce
  ! an element symbol if it is present.
  ! Requires: coords -- vector type containing position coordinate
  !           element -- the element symbol
  !           ilabel -- optional integer used to get element symbol
  !----------------------------------------------------------------------------
  Type(XYZ_Entry) Function visxyz_make(coords,element,ilabel)
    Type(VecType), Intent(In)          :: coords
    Character(*), Intent(In)           :: element
    Integer, Optional, Intent(In)      :: ilabel

    Integer                            :: index
    
    visxyz_make%r = coords
    If (Present(ilabel)) Then
      index = Mod(ilabel,10)
      visxyz_make%symbol = visxyz_molabel2(index)
    Else
      visxyz_make%symbol = element(1:2)
    End If

  End Function visxyz_make

  !----------------------------------------------------------------------------
  ! Makes a set of frames starting with the passed set, a displacement set.  
  ! Subsequent frames are generated by displacements along a 3N-vector.  
  ! This is useful, for example, for generating movies of normal mode 
  ! displacements.
  ! Requires:  entries -- an array containing XYZ entires
  !            dispvecs -- translation vector for each XYZ entry
  !            nframes -- the number of frames to create in each direction
  !            filename -- filename to dump to
  !            display_format -- display format for coordinates only
  !            comment -- optional comment
  !----------------------------------------------------------------------------
  Subroutine visxyz_dispset(entries,dispvecs,nframes,filename, &
      display_format,comment)
    Type(XYZ_Entry), Dimension(:), Intent(In)    :: entries
    Type(VecType), Dimension(:), Intent(In)      :: dispvecs
    Integer, Intent(In)                          :: nframes
    Character(*), Intent(In)                     :: filename,display_format
    Character(*), Optional                       :: comment

    Integer                                    :: i,frame
    Real(kind=RDbl)                            :: scale
    Character(len=lstrLen)                     :: final_comment
    Type(XYZ_Entry), Dimension(Size(entries))  :: coords

    !** set the comment
    If (.NOT.(Present(comment))) Then
      Write(final_comment,'(a)') 'Displacement set, created by MUSIC code'
    Else
      final_comment = comment
    End If 

    !** make sure the entries and transvec sizes agree
    If ((Size(entries)) /= Size(dispvecs)) Then
      Write(0,'(2a,i4,a)') __FILE__,": ",__LINE__, &
          ' number of atoms and displacements vectors do not agree '
      Write(0,'(2i4)') Size(entries),Size(dispvecs)
      Stop  
    End If

    coords = entries

    !** go backwards along the displacement vectors
    Do frame = nframes,1,-1
      scale = frame
      Do i = 1,Size(coords)
        coords(i)%r = entries(i)%r - scale*dispvecs(i)
      End Do
      Call visxyz_dump(coords,filename,display_format,final_comment)
    End Do

    Call visxyz_dump(entries,filename,display_format,final_comment)

    !** go forwards along the displacement vectors    
    Do frame = 1,nframes
      Do i = 1,Size(coords)
        coords(i)%r = coords(i)%r + dispvecs(i)        
      End Do
      Call visxyz_dump(coords,filename,display_format,final_comment)
    End Do

  End Subroutine visxyz_dispset

  !----------------------------------------------------------------------------
  ! This function turns an entry into a displayable string
  ! Requires:  item -- an XYZ entry
  !            fmt -- optional format for number
  !----------------------------------------------------------------------------
!!$  Character(len=lstrLen) Function visxyz_entrydisplay(item,fmt)
  Function visxyz_entrydisplay(item,fmt)
    Character(len=lstrLen)                :: visxyz_entrydisplay
    Type(XYZ_Entry), Intent(In)           :: item
    Character(*), Intent(In), Optional    :: fmt

    Character(len=strLen)                 :: strformat
    Character(len=lstrLen)                :: string

    If (Present(fmt)) Then
      Write(strformat, '(a)') "(1x,a,3x,a)"
      string = vector_display(item%r,fmt)
      Write(visxyz_entrydisplay, strformat) item%symbol,Trim(string)
    Else
      string = vector_display(item%r)
      Write(visxyz_entrydisplay,'(a,2x,a)') item%symbol,Trim(string)
    End If

    visxyz_entrydisplay = Adjustl(visxyz_entrydisplay)

  End Function visxyz_entrydisplay

  !----------------------------------------------------------------------------
  ! Dump information for a set of xyz file entries.  The xyz file format is:
  ! line1: number_of_atoms_in_frame
  ! line2: blank line or comment
  ! line3-N: element_symbol  x_coordinate  y_coordinate  z_coordinate
  ! NOTE: this can be repeated in the same file to create a movie 
  ! Requires:  entries -- an array containing XYZ entires
  !            filename -- filename to dump to
  !            display_format -- optional display format for coordinates only
  !            comment -- optional comment
  !----------------------------------------------------------------------------
  Subroutine visxyz_dump(entries,filename,display_format,comment)
    Type(XYZ_Entry), Dimension(:), Intent(In)    :: entries
    Character(*), Intent(In)                     :: filename
    Character(*), Intent(In), Optional           :: display_format
    Character(*), Intent(In), Optional           :: comment

    Integer                              :: nentries,unit,i
    Character(len=lstrLen)               :: final_comment,string

    !** set the comment
    If (.NOT.(Present(comment))) Then
      Write(final_comment,'(a)') 'Created by MUSIC code'
    Else
      final_comment = comment
    End If 

    nentries = Size(entries)

    !** dump the file
!    Write(*,'(3a)') __FILE__': Opening file: ',Trim(filename)
    unit = file_open(filename)

    Write(unit,*) nentries
    Write(unit,*) Trim(final_comment)
    Do i = 1,nentries
      If (Present(display_format)) Then
        If (Trim(display_format) /= 'NULL') Then
          string = visxyz_entrydisplay(entries(i),display_format)
          Write(unit,'(a,i5)') Trim(string),i
        End If
      Else 
        string = visxyz_entrydisplay(entries(i))
        Write(unit,'(a,i5)') Trim(string),i
      End If
    End Do

  End Subroutine visxyz_dump

End Module visxyz










